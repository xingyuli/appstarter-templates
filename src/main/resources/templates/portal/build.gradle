import groovy.xml.MarkupBuilder

buildscript {
    repositories {
        maven {
            url "http://artifactory.mobisist.com/artifactory/libs-all"
        }
    }
}

plugins {
    id "org.springframework.boot" version "1.5.6.RELEASE"
}

apply plugin: 'war'

dependencies {
    compile project(':web-common')

    runtime "mysql:mysql-connector-java:$mysql_connector_version"

    testCompile "io.springfox:springfox-staticdocs:$springfox_version"
}

task(javascriptGenerator, type: JavaExec) {
    main = 'com.mobisist.springbootseed.appstarter.infra.web.gen.JavascriptGeneratorKt'

    def writer = new StringWriter()
    new MarkupBuilder(writer).configuration() {
        apiConstantsGenerator() {
            destinationFile(project.file("src/main/webapp/assets/external/js/common/api_constants.js").absolutePath)
            controllerPackage("@packageName@.portal.controller")
            customErrorCodeClass("@packageName@.infra.service.CustomErrorCode")
        }
        optionsGenerator() {
            destinationFile(project.file("src/main/webapp/assets/external/js/common/options.js").absolutePath)
            enumPackage('@packageName@.infra.db.enums')
        }
    }
    systemProperties['javascriptGenerator'] = writer.toString()

    classpath = sourceSets.main.runtimeClasspath
}

gradle.projectsEvaluated {
    processResources.dependsOn(copySql)
    bootRun.dependsOn(javascriptGenerator)
    war.dependsOn(javascriptGenerator)
}
